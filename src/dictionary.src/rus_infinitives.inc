 // ****************************************************************************
 // Макрос добавляет признак 'управляет инфинитивом'
 // Например - инфинитив РАЗРЕШИТЬ допускает конструкции типа 'РАЗРЕШИТЬ СПАТЬ'
 // ****************************************************************************
 #define модальный(Инф) tag ИНФИНИТИВ:Инф { МОДАЛЬНЫЙ }


 // *************************************************************
 // Для переходных глаголов - с указанием падежной валентности
 // *************************************************************
 #define сов_инф( имя, В_Несов=NULL, Падежи=NULL )      \
  #begin

  #if Падежи!=NULL && Падежи!=INTR
  entry имя : ИНФИНИТИВ { ВИД:СОВЕРШ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:Падежи }
  #else
  entry имя : ИНФИНИТИВ { ВИД:СОВЕРШ ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ }
  #endif

  #if В_Несов!=NULL
   #begin

    #if В_Несов==Вин || В_Несов==Твор || В_Несов==Дат
     #error Ошибка в определеннии инфинитива: имя
    #endif

    extern entry В_Несов : ИНФИНИТИВ
    link ИНФИНИТИВ:имя {ВИД:СОВЕРШ}     <в_несов> ИНФИНИТИВ:В_Несов{ВИД:НЕСОВЕРШ}
    link ИНФИНИТИВ:В_Несов {ВИД:НЕСОВЕРШ} <в_сов>   ИНФИНИТИВ:имя{ВИД:СОВЕРШ}

    // Если падежная валентность не определена, то импортируем из инфинитива несовершенного вида 
    #if Падежи==NULL
     tag ИНФИНИТИВ:имя { replace:ПЕРЕХОДНОСТЬ:<в_несов> replace:ПАДЕЖ:<в_несов> }
    #endif

   #end
  #endif

  #end



 #define сов_инф_удар( имя, ударение, В_Несов=NULL, Падежи=NULL )      \
  #begin

  #if Падежи!=NULL && Падежи!=INTR
  entry имя : ИНФИНИТИВ { ВИД:СОВЕРШ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:Падежи aux stress=ударение }
  #else
  entry имя : ИНФИНИТИВ { ВИД:СОВЕРШ ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ aux stress=ударение }
  #endif

  #if В_Несов!=NULL
   #begin

    #if В_Несов==Вин || В_Несов==Твор || В_Несов==Дат
     #error Ошибка в определеннии инфинитива: имя
    #endif

    extern entry В_Несов : ИНФИНИТИВ
    link ИНФИНИТИВ:имя {ВИД:СОВЕРШ}     <в_несов> ИНФИНИТИВ:В_Несов{ВИД:НЕСОВЕРШ}
    link ИНФИНИТИВ:В_Несов {ВИД:НЕСОВЕРШ} <в_сов>   ИНФИНИТИВ:имя{ВИД:СОВЕРШ}

    // Если падежная валентность не определена, то импортируем из инфинитива несовершенного вида 
    #if Падежи==NULL
     tag ИНФИНИТИВ:имя { replace:ПЕРЕХОДНОСТЬ:<в_несов> replace:ПАДЕЖ:<в_несов> }
    #endif

   #end
  #endif

  #end


 // ************************************************************
 // Для переходных глаголов - с указанием падежной валентности
 // ************************************************************
 #define несов_инф( имя, В_Сов=NULL, Падежи=NULL )      \
  #begin
  
   #if Падежи!=NULL
   entry имя : ИНФИНИТИВ { ВИД:НЕСОВЕРШ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:Падежи }
   #else
   entry имя : ИНФИНИТИВ { ВИД:НЕСОВЕРШ ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ }
   #endif


   #if В_Сов!=NULL
    #begin

     #if В_Сов==Вин
      #error Ошибка в определении инфинитива: имя
     #endif

     extern entry В_Сов : ИНФИНИТИВ
     link ИНФИНИТИВ:имя { ВИД:НЕСОВЕРШ }   <в_сов>   ИНФИНИТИВ:В_Сов{ ВИД:СОВЕРШ }
     link ИНФИНИТИВ:В_Сов { ВИД:СОВЕРШ } <в_несов> ИНФИНИТИВ:имя{ ВИД:НЕСОВЕРШ }
    #end
   #endif
  #end

 // Дополнительный аргумент - ударность
 #define несов_инф_удар( имя, ударение, В_Сов=NULL, Падежи=NULL )      \
  #begin
  
   #if Падежи!=NULL
   entry имя : ИНФИНИТИВ { ВИД:НЕСОВЕРШ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:Падежи aux stress=ударение }
   #else
   entry имя : ИНФИНИТИВ { ВИД:НЕСОВЕРШ ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ aux stress=ударение }
   #endif

   #if В_Сов!=NULL
    #begin

     #if В_Сов==Вин
      #error Ошибка в определении инфинитива: имя
     #endif

     extern entry В_Сов : ИНФИНИТИВ
     link ИНФИНИТИВ:имя { ВИД:НЕСОВЕРШ }   <в_сов>   ИНФИНИТИВ:В_Сов{ ВИД:СОВЕРШ }
     link ИНФИНИТИВ:В_Сов { ВИД:СОВЕРШ } <в_несов> ИНФИНИТИВ:имя{ ВИД:НЕСОВЕРШ }
    #end
   #endif
  #end


 // ***********************************************************************
 // Макрос для облегчения набора статей для глаголов, не имеющих подходящей
 // таблицы словообразования
 // ***********************************************************************
 #define VEntry(                   \
                Name, Aspect,      \
                F1, F2,            \
                F3, F4,            \
                F5, F6,            \
                F7, F8, F9, F10,   \
                F11,               \
                F12, F13           \
              )                    \
 #begin
 entry Name : ГЛАГОЛ
 {
  ВИД : Aspect
  НАКЛОНЕНИЕ:ИЗЪЯВ |{

  #if Aspect==НЕСОВЕРШ
                     ВРЕМЯ:НАСТОЯЩЕЕ |{
  #else
                     ВРЕМЯ:БУДУЩЕЕ   |{
  #endif
                                       ЛИЦО:1 ЧИСЛО { F1 F2 }
                                       ЛИЦО:2 ЧИСЛО { F3 F4 }
                                       ЛИЦО:3 ЧИСЛО { F5 F6 }
                                      }

                     ВРЕМЯ:ПРОШЕДШЕЕ |{
                                       ЧИСЛО:ЕД РОД { F7 F8 F9 }
                                       ЧИСЛО:МН     { F10 }
                                      }
                    }

  НАКЛОНЕНИЕ:ПОБУД ЛИЦО:2 ЧИСЛО:ЕД { F11 }
  НАКЛОНЕНИЕ:ПОБУД ЛИЦО:2 ЧИСЛО:МН { F12 }
  НАКЛОНЕНИЕ:ПОБУД ЛИЦО:1 ЧИСЛО:МН { F13 }


  <в_инфинитив> ИНФИНИТИВ:Name{ ВИД : Aspect }
 }
 #end
  

